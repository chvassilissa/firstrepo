Функция ФайлИлиКаталогСуществует(КаталогПоиска, ИмяФайлаИлиКаталога)

    МассивФайлов = НайтиФайлы(КаталогПоиска, ИмяФайлаИлиКаталога);
    Если МассивФайлов.Количество() > 0 Тогда
        Возврат Истина;
    КонецЕсли; 
    Возврат Ложь;
    
КонецФункции

Процедура СкопироватьФайлыИКаталогССодержимым(Источник, Приемник)

    Разделитель = ПолучитьРазделительПути();
    
    Файл = Новый Файл(Источник);
    Если Файл.ЭтоКаталог() тогда
    
        СоздатьКаталог(Приемник);
        МассивФайлов = НайтиФайлы(Источник, "*");
        Для каждого файл из МассивФайлов Цикл
            
            Если Файл.ЭтоФайл() Тогда
                КопироватьФайл(Файл.ПолноеИмя, Приемник + Разделитель + Файл.Имя);  
            Иначе
                СкопироватьФайлыИКаталогССодержимым(Файл.ПолноеИмя, Приемник + Разделитель + Файл.Имя);   
            КонецЕсли;        
        
        КонецЦикла;
    Иначе
        КопироватьФайл(Источник, Приемник);    
    КонецЕсли;    
КонецПроцедуры    

Функция ПолучитьМассивФайловДляКопирования()
    
    МассивФайловДляКопирования = Новый Массив;
    МассивФайловДляКопирования.Добавить("pre-commit");
    МассивФайловДляКопирования.Добавить("v8files-extractor.os");
    МассивФайловДляКопирования.Добавить("ibService");
    МассивФайловДляКопирования.Добавить("tools");
    МассивФайловДляКопирования.Добавить("v8Reader");
    
    Возврат МассивФайловДляКопирования;
        
Конецфункции

Процедура ЗапуститьПриложениеИОбработатьРезультат(Команда, Каталог, ТекстСообщенияОбОшибке)
    
    КодВозварата = "";
    ЗапуститьПриложение(Команда, Каталог,Истина, КодВозварата);
    Если КодВозварата <> 0 Тогда
        Сообщить(ТекстСообщенияОбОшибке + " (КодВозврата = " + КодВозварата + ")");
        Приостановить(30000);       
        ЗавершитьРаботу(0);
    КонецЕсли;  
    
КонецПроцедуры    
     

КаталогНовогоРепозитория = ТекущийКаталог();
Разделитель = ПолучитьРазделительПути();

//Инициализация репозитория git
ЗапуститьПриложениеИОбработатьРезультат("git init", КаталогНовогоРепозитория, "Не удалось инициализировать новый репозиторий");
Сообщить("Репозиторий " + КаталогНовогоРепозитория + " успешно инициализирован.");

ЗапуститьПриложениеИОбработатьРезультат("git config --local core.quotepath false", КаталогНовогоРепозитория, "Не удалось включить поддержку кириллических имен файлов");

//Находим родительский каталог, в котором находятся все репозитории, для работы с дополнительными репозиториями
ПозицияРазделителя = 0;
Пока СтрНайти(КаталогНовогоРепозитория, Разделитель,,ПозицияРазделителя+1) > 0 Цикл
    ПозицияРазделителя = СтрНайти(КаталогНовогоРепозитория, Разделитель,,ПозицияРазделителя+1);
КонецЦикла; 

Если ПозицияРазделителя <> 0 Тогда
    //удалось найти каталог с репозиториями   
    КаталогВсехРепозиториев = Лев(КаталогНовогоРепозитория, ПозицияРазделителя-1);
    
    //ищем репозиторий прекоммит
    precommit1c = "precommit1c";   
    КаталогPrecommit1c = КаталогВсехРепозиториев + Разделитель + precommit1c;
    
    //если не нашли, то создаем репозиторий прекоммит клонированием с гитхаба
    Если не ФайлИлиКаталогСуществует(КаталогВсехРепозиториев, precommit1c) Тогда

        ЗапуститьПриложениеИОбработатьРезультат("git clone https://github.com/xDrivenDevelopment/precommit1c.git", КаталогНовогоРепозитория, "Не удалось клонировать репозиторий precommit1c");
        Сообщить("Репозиторий precommit1c успешно склонирован.");
        
    КонецЕсли;    
    
    //создаем каталог hooks при необходимости
    Если не ФайлИлиКаталогСуществует(КаталогНовогоРепозитория + Разделитель + ".git", "hooks") тогда
        СоздатьКаталог(КаталогНовогоРепозитория + Разделитель + ".git" + Разделитель + "hooks");
        Сообщить("Создан каталог " + КаталогНовогоРепозитория + Разделитель + ".git" + Разделитель + "hooks");
    КонецЕсли;
    
    //получаем массив имен файлов и каталогов для копирования с репозитория прекоммит
    МассивФайловДляКопирования = ПолучитьМассивФайловДляКопирования();
    
    //копируем недостающие файлы/каталоги
    Для Каждого ИмяФайла из МассивФайловДляКопирования Цикл
        Если ФайлИлиКаталогСуществует(КаталогPrecommit1c, ИмяФайла) Тогда
            СкопироватьФайлыИКаталогССодержимым(КаталогPrecommit1c + Разделитель + ИмяФайла, КаталогНовогоРепозитория + Разделитель + ".git" + Разделитель + "hooks" + Разделитель + ИмяФайла);
            Сообщить("Скопирован файл " + ИмяФайла);    
        КонецЕсли;    
    КонецЦикла;    
    
    //Подключаем vanessa-bootstrap
    ЗапуститьПриложениеИОбработатьРезультат("git remote add vanessa-bootstrap https://github.com/silverbulleters/vanessa-bootstrap.git", КаталогНовогоРепозитория, "Ошибка выполнения команды");
    ЗапуститьПриложениеИОбработатьРезультат("git config --local merge.ours.driver true", КаталогНовогоРепозитория, "Ошибка выполнения команды");
    ЗапуститьПриложениеИОбработатьРезультат("git fetch --no-tags vanessa-bootstrap", КаталогНовогоРепозитория, "Ошибка выполнения команды");
    ЗапуститьПриложениеИОбработатьРезультат("git pull --no-tags --no-commit vanessa-bootstrap master", КаталогНовогоРепозитория, "Ошибка выполнения команды");
    Сообщить("Каталоги vanessa-bootstrap созданы усешно");  
    Сообщить("Выполнение скрипта успешно завершено");   
Иначе    
    Сообщить("Не удалось найти каталог хранения репозиториев. Файлы precommit1c не скопированы.")    
КонецЕсли;
Приостановить(30000);